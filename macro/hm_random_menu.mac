/*秀丸エディタから乱数列を生成するマクロ

以下をメニューから選択します。
	＊生成する個数

	＊乱数の種類
		xorshift
		xor
		xor64
		xorwow
		sfmt(SIMD-oriented Fast Mersenne Twister.)

	＊最大最小値の基数
		2
		10
		16

	＊最小値
	
	＊最大値

	＊プレフィックス

	＊出力乱数の基数
		2
		10
		16

	＊区切り
		改行
		カンマ
		空白


生成した乱数は新しい秀丸エディタに追加されます。
*/
$g_rnd_num		="";
$g_rnd_func		="";
$g_rnd_in_base	="";
$g_rnd_min		="";
$g_rnd_max		="";
$g_rnd_prefix	="";
$g_rnd_out_base	="";
$g_rnd_delimiter="";


////////////////////////////////////////////////////////////////////////////
$g_tbl_ord[0]		= "2進法";
$g_tbl_ord_str[0]	= "2";
$g_tbl_ord[1]		= "10進法";
$g_tbl_ord_str[1]	= "10";
$g_tbl_ord[2]		= "16進法";
$g_tbl_ord_str[2]	= "16";
#g_tbl_ord_num=3;


////////////////////////////////////////////////////////////////////////////
$g_tbl_delimiter[0]		= "改行";
$g_tbl_delimiter_str[0]	= "\\n";
$g_tbl_delimiter[1]		= "カンマ";
$g_tbl_delimiter_str[1]	= ",";
$g_tbl_delimiter[2]		= "空白";
$g_tbl_delimiter_str[2]	= " ";
$g_tbl_delimiter[3]		= "無し";
$g_tbl_delimiter_str[3]	= "";
#g_tbl_delimiter_num	= 4;


////////////////////////////////////////////////////////////////////////////
$g_tbl_name[0]="xorshift[xor128](周期 2^128-1)";
$g_tbl_func[0]="xorshift";
$g_tbl_name[1]="xorshift[xor](周期 2^32-1)";
$g_tbl_func[1]="xor";
$g_tbl_name[2]="xorshift[xor64](周期 2^64-1)";
$g_tbl_func[2]="xor64";
$g_tbl_name[3]="xorshift[xorwow](周期 2^192-2^32)";
$g_tbl_func[3]="xorwow";
$g_tbl_name[4]="SIMD-oriented Fast Mersenne Twister(周期 2^19937-1)";
$g_tbl_func[4]="sfmt";
#g_num_tbl_name=5;


#g_dengaku_state=false;

call Main;
if(#g_dengaku_state){
	freedll;
}
endmacro str(result);


Main:
	call LoadDengaku;
	if(! result){
		return false;
	}
	call ShowDialog;
	if(! ##return){
		return false;
	}


	#num = val($g_rnd_num);
	if(#num<=0){
		return false;
	}
	execmacro currentmacrodirectory+"\\hm_random_main.mac",
		$g_rnd_num,
		$g_rnd_func,
		$g_rnd_in_base,
		$g_rnd_min,
		$g_rnd_max,
		$g_rnd_prefix,
		$g_rnd_out_base,
		$g_rnd_delimiter;
	return val(getresultex(-1));


ShowDialog:
	##n = dllfunc("NEWDIALOG","コンボボックス・ドロップダウンリストのテスト",60);

	##n = dllfunc("NEWCONTROL","text","txt1","生成する個数");
	##n = dllfunc("NEWCONTROL","edit","num","10");

	call MakeMenu "乱数の種類", "func", 0;
	if(! result){
		return false;
	}
	
	call MakeMenu "最大最小値の基数", "in_base", 1;
	if(! result){
		return false;
	}
	
	##n = dllfunc("NEWCONTROL","text","txt2","最小値");
	##n = dllfunc("NEWCONTROL","edit","min","0");
	
	##n = dllfunc("NEWCONTROL","text","txt3","最大値");
	##n = dllfunc("NEWCONTROL","edit","max","100");
	
	##n = dllfunc("NEWCONTROL","text","txt4","プレフィックス");
	##n = dllfunc("NEWCONTROL","edit","prefix","hoge");
	
	call MakeMenu "乱数の基数", "out_base", 1;
	if(! result){
		return false;
	}
	
	call MakeMenu "区切り", "delimiter", 2;
	
	##n = dllfunc("NEWCONTROL","okcancel","","");

	if(! dllfunc("SHOWDIALOG",hidemaruhandle(0),1)){
		return false;
	}

	//イベントループ
	$name = "";
	while(1){
		while (strlen($name) == 0) {
			$name = dllfuncstr("WAITCTRLNOTIFY",10);
		}
		##notify = val($name);
		if (##notify <= 1) break; // 「ＯＫ」または「キャンセル」
	}
	if (!dllfunc("ENDDIALOG")) goto ERROR;
	if (leftstr($name,1) != "1"){
		//キャンセル
		return false;
	}
	
	call SetDialogValueToGlobalValue;
	##ret = ##return;
	return ##ret;


MakeMenu:
	$$title		= $$1;
	$$value_name= $$2;
	##no 		= ##3;
	
	if(0==##no){
		##menu_num = #g_num_tbl_name;
	}else if(1==##no){
		##menu_num = #g_num_tbl_name;
	}else if(2==##no){
		##menu_num = #g_tbl_delimiter_num;
	}
	
	##n = dllfunc("NEWCONTROL","text","",$$title);
	##n = dllfunc("SETCTRLWIDTH","",15);
	##n = dllfunc("NEWCONTROL","cmblist",$$value_name,"");

	##i = 0;
	while(##i < ##menu_num){
		if(0==##no){
			$$menu_name = $g_tbl_name[##i];
		}else if(1==##no){
			$$menu_name = $g_tbl_ord[##i];
		}else if(2==##no){
			$$menu_name = $g_tbl_delimiter[##i];
		}
		
		##n = dllfunc("SETCTRLITEM","",$$menu_name,"-1");
		##i=##i+1;
	}
	
	//とりあえず、先頭の項目を選択しておく。
	##n=dllfunc("SETCTRLSTATE",$$value_name, "1");
	return true;

SetDialogValueToGlobalValue:
	$g_rnd_num 		= dllfuncstr("GETCTRLSTRING","num");
	
	$$func 			= dllfuncstr("GETCTRLSTATE","func");
	if("0"==$$func){
		return false;
	}
	$g_rnd_func 	= $g_tbl_func[val($$func)-1];
	
	$$in_base 		= dllfuncstr("GETCTRLSTATE","in_base");
	if("0"==$$in_base){
		return false;
	}
	$g_rnd_in_base 	= $g_tbl_ord_str[val($$in_base)-1];
	
	$g_rnd_min 		= dllfuncstr("GETCTRLSTRING","min");
	$g_rnd_max 		= dllfuncstr("GETCTRLSTRING","max");
	$g_rnd_prefix	= dllfuncstr("GETCTRLSTRING","prefix");
	
	$$out_base 		= dllfuncstr("GETCTRLSTATE","out_base");
	if("0"==$$out_base){
		return false;
	}
	$g_rnd_out_base	= $g_tbl_ord_str[val($$out_base)-1];
	
	$$delimiter		= dllfuncstr("GETCTRLSTATE","delimiter");
	if("0" == $$delimiter){
		return false;
	}
	$g_rnd_delimiter= $g_tbl_delimiter_str[val($$delimiter)-1];
	return true;
	
	
LoadDengaku:
	#g_dengaku_state=false;
	loaddll hidemarudir + "\\DengakuDLL.dll";
	if (!result) {
		message "DLL をロードできませんでした。";
		return false;
	}
	$DLLVer = dllfuncstr("GETVERSION");
	if (val(leftstr($DLLVer, 1)) < 2) {
		message "このマクロの実行には Ver.2.00 以降の田楽ＤＬＬが必要です。";
		freedll;
		return false;
	}
	#g_dengaku_state=true;
	return true;
