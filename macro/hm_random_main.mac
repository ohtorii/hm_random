/*秀丸エディタから乱数列を生成するマクロ
*/

$g_exe_filename = currentmacrodirectory + "\\hm_random.exe";
#g_num_arg 		= 3;


$g_searchbuffer = searchbuffer;
#g_searchoption = searchoption;
//折りたたみを維持,部分編集を維持,範囲選択,最後に移動
//マクロ終了時に元に戻る。
setcompatiblemode 0x0003|0x000c|0x0200|0x2000;
call Main;
setsearch $g_searchbuffer, #g_searchoption; 
endmacro;


Main:
	if(! existfile($g_exe_filename)){
		message("実行ファイルが見つかりません。\n" +
				"ファイル名：" + $g_exe_filename);
		return false;
	}
	
	if(#g_num_arg != argcount){
		message("引数の個数が合いません");
		return false;
	}
	
	$$arg	= " " + getarg(0) + " " + getarg(1);
	call CorrectFormat getarg(2);
	$$arg	= $$arg + " " + $$return;
	
	$$exe = "\"" + $g_exe_filename + "\"";
	runex $$exe + $$arg
		, 0 			//sync	  0:async 1:sync
		, 0, "" 		//stdin   0:none 1:auto 2:file 3:(reserve) 4:all 5:select
		, 4, "" 		//stdout  0:none 1:auto 2:file 3:add file  4:new 5:insert 6:replace
		, 0, "" 		//stderr  0:none 1:=out 2:file 3:add file  4:new 5:insert 6:replace
		, 0, "" 		//folder  0:none 1:current 2:specify 3:(reserve) 4:exe's folder
		, 2 			//show	  0:auto 1:show 2:hide
		, 1 			//nodraw  0:draw 1:no draw
		, 0 			//unicode 0:ansi 2:unicode
		;
	##ret = result;
	if(false == ##ret){
		message("runexで失敗");
		return false;
	}
	
	return true;


CorrectFormat:
	$$fmt = $$1;
	
	##old_hidemaru = hidemaruhandle(0);
	openfile "/h ";
	##new_hidemaru = hidemaruhandle(0);
	
	disabledraw;
	insert($$fmt);
	gofiletop;
	replaceallfast "%", "%%", noregular;
	enabledraw;

	selectall;
	$$text = gettext2(seltopcolumn,seltoplineno,selendcolumn,selendlineno);
	setactivehidemaru ##old_hidemaru;
	closehidemaruforced ##new_hidemaru;
	
	return $$text;