/*秀丸エディタから乱数列を生成するマクロ
マクロで内部使用するファイル。

（やっていること）
乱数を生成する実行ファイルを呼び出し、標準出力を秀丸エディタへ反映する。
*/


$g_exe_filename = "hm_random.exe";

//exeに与える引数の個数
#g_num_arg		= 8;


call Start;
endmacro str(result);




Start:
	$g_searchbuffer = searchbuffer;
	#g_searchoption = searchoption;
	call Main;
	##ret = result;
	setsearch $g_searchbuffer, #g_searchoption;
	return ##ret;


Main:
	if(! existfile($g_exe_filename)){
		message("実行ファイルが見つかりません。\n" +
				"ファイル名：" + $g_exe_filename);
		return false;
	}

	if(#g_num_arg != argcount){
		message("引数の個数が合いません");
		return false;
	}

	$$arg	= " " + getarg(0) + " " + getarg(1) + " " + getarg(2) + " " + getarg(3) + " " + getarg(4);
	call CorrectFormat getarg(5);
	$$arg	= $$arg + " " + "\"" + $$return + "\"";
	$$arg	= $$arg + " " + getarg(6);
	call CorrectFormat getarg(7);
	$$arg	= $$arg + " " + "\"" + $$return + "\"";

	message($$arg);
	$$exe = "\"" + $g_exe_filename + "\"";
	runex $$exe + $$arg
		, 1 			//sync	  0:async 1:sync
		, 0, "" 		//stdin   0:none 1:auto 2:file 3:(reserve) 4:all 5:select
		, 4, "" 		//stdout  0:none 1:auto 2:file 3:add file  4:new 5:insert 6:replace
		, 0, "" 		//stderr  0:none 1:=out 2:file 3:add file  4:new 5:insert 6:replace
		, 0, "" 		//folder  0:none 1:current 2:specify 3:(reserve) 4:exe's folder
		, 2 			//show	  0:auto 1:show 2:hide
		, 1 			//nodraw  0:draw 1:no draw
		, 0 			//unicode 0:ansi 2:unicode
		;
	##ret = result;
	if(false == ##ret){
		message("runexで失敗");
		return false;
	}

	return true;

/*秀丸エディタは 「%d -> フォルダ名」へ置換するので、% -> %%へ修正。

詳細は秀丸マクロのヘルプを参照。
run，runsync，runsync2文
*/
CorrectFormat:
	$$fmt = $$1;

	##old_hidemaru = hidemaruhandle(0);
	openfile "/h";
	##new_hidemaru = hidemaruhandle(0);

	disabledraw;
	insert($$fmt);
	gofiletop;
	replaceallfast "%", "%%", noregular;
	enabledraw;

	selectall;
	$$text = gettext2(seltopcolumn,seltoplineno,selendcolumn,selendlineno);
	setactivehidemaru ##old_hidemaru;
	closehidemaruforced ##new_hidemaru;

	return $$text;
